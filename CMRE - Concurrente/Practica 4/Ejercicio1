{-----------------------------}
{Ejercicio: Cliente y Servidor}
{-----------------------------}
programa ejercicio1
procesos
  {Procesos para el servidor}
  proceso recibirCoordenadas(E id:numero;ES posX:numero;ES posY:numero)
  comenzar
    si(id=1)
      RecibirMensaje(posX,cliente1)
      RecibirMensaje(posY,cliente1)
    sino
      si(id=2)
        RecibirMensaje(posX,cliente2)
        RecibirMensaje(posY,cliente2)
      sino
        RecibirMensaje(posX,cliente3)
        RecibirMensaje(posY,cliente3)
  fin
  proceso comunicar_Robot(E id:numero)
  comenzar
    si(id=1)
      EnviarMensaje(V,cliente1)
    sino
      si(id=2)
        EnviarMensaje(V,cliente2)
      sino
        EnviarMensaje(V,cliente3)
  fin
  proceso filtrarMensaje(ES id:numero; ES cant:numero)
  variables
    ids:numero
  comenzar
    RecibirMensaje(ids,*)
    si(ids=1)
      id := 1
      RecibirMensaje(cant,cliente1)
    sino
      si(ids=2)
        id := 2
        RecibirMensaje(cant,cliente2)
      sino
        id := 3
        RecibirMensaje(cant,cliente3)
  fin
  proceso enviarIds
  comenzar
    EnviarMensaje(1,cliente1)
    EnviarMensaje(2,cliente2)
    EnviarMensaje(3,cliente3)
  fin
  {Procesos para clientes}
  proceso calcularRandomFlores(ES flores: numero)
  variables
    rand:numero
  comenzar
    Random(rand,1,4)
    flores := rand
  fin
  proceso comunicarConServidor(E id:numero ; E cant:numero ; E Av:numero ; E Ca:numero)
  comenzar
    EnviarMensaje(id,servidor)
    EnviarMensaje(cant,servidor)
    EnviarMensaje(Av,servidor)
    EnviarMensaje(Ca,servidor)
  fin
  proceso juntarFlores
  comenzar
    mientras HayFlorEnLaEsquina
      tomarFlor
  fin
  proceso avanzarFlores
  comenzar
    mientras ((PosCa<100)&(HayFlorEnLaBolsa))
      depositarFlor
      mover
    si((PosCa=100)&(HayFlorEnLaBolsa))
      depositarFlor
  fin
areas
  pista1: AreaPC (1,1,1,100)
  pista2: AreaPC (2,1,2,100)
  pista3: AreaPC (3,1,3,100)
  base: AreaP (100,100,100,100)
robots
  robot cliente
  variables
    id:numero
    floresPedir:numero
    ack:boolean {booleano de operacion del servidor completado}
  comenzar
    {Inicializacion}
    RecibirMensaje(id,servidor)
    {--Procesos--}
    mientras(PosCa<100)
      calcularRandomFlores(floresPedir)
      {comunicacion al server}
      comunicarConServidor(id,floresPedir,PosAv,PosCa+1)
      RecibirMensaje(ack,servidor)
      {Recepcion de flores}
      Pos(PosAv,PosCa+1)
      juntarFlores
      Pos(PosAv,PosCa-1)
      avanzarFlores
    comunicarConServidor(id,0,0,0)
  fin
  robot server
  variables
    avIni:numero
    caIni:numero
    cant:numero
    posX:numero
    posY:numero
    id:numero
    clientesDispo:numero
  comenzar
    clientesDispo:=3
    enviarIds
    mientras (clientesDispo>0)
      filtrarMensaje(id,cant)
      si(cant > 0)
        recibirCoordenadas(id,posX,posY)
        avIni := PosAv
        caIni := PosCa
        repetir cant
          tomarFlor
        Pos(posX,posY)
        repetir cant
          depositarFlor
        Pos(avIni,caIni)
        comunicar_Robot(id)
      sino
        clientesDispo := clientesDispo - 1
  fin
variables
  cliente1: cliente
  cliente2: cliente
  cliente3: cliente
  servidor: server
comenzar
  AsignarArea(cliente1, pista1)
  AsignarArea(cliente2, pista2)
  AsignarArea(cliente3, pista3)
  AsignarArea(servidor, pista1)
  AsignarArea(servidor, pista2)
  AsignarArea(servidor, pista3)
  AsignarArea(servidor, base)
  Iniciar(cliente1, 1,1)
  Iniciar(cliente2, 2,1)
  Iniciar(cliente3, 3,1)
  Iniciar(servidor,100,100)
fin