{A diferencia de la solucion anterior en el que los trabajadores realizaban las tareas de forma lineal o secuencial}
{En este programa los robots lo hacen en forma de pila}
{De modo que el jefe envia todas las intrucciones y finaliza}
{Y los trabajadores reciben "de a poco" los mensajes hasta finalizar con la instruccion 4 por el jefe}
{Lo que difiere con la solucion anterior es que los mismos no avisan al jefe que terminaron. Lo que hace que la ejecucion deje de ser secuencial}
programa ejercicio4b
{-------------------}
{Jefe y trabajadores}
{-------------------}
procesos
  proceso recogerFlores(ES cant: numero)
  comenzar
    mientras HayFlorEnLaEsquina
      tomarFlor
      cant := cant + 1
  fin
  proceso recogerPapeles(ES cant: numero)
  comenzar
    mientras HayPapelEnLaEsquina
      tomarPapel
      cant := cant + 1
  fin
  proceso vaciarBolsa(E flores:numero;E papeles:numero)
  comenzar
    repetir flores
      depositarFlor
    flores := 0
    repetir papeles
      depositarPapel
    papeles := 0
  fin
  proceso procesarEsquina(E tareaNum:numero;E av:numero;E ca:numero;ES flores:numero;ES papeles:numero)
  variables
    avIni:numero
    caIni:numero
  comenzar
    avIni := PosAv
    caIni := PosCa
    BloquearEsquina(av,ca)
    Pos(av,ca)
    si(tareaNum = 1)
      recogerFlores(flores)
    sino
      si(tareaNum = 2)
        recogerPapeles(papeles)
      sino
        vaciarBolsa(flores,papeles)
    Pos(avIni,caIni)
    LiberarEsquina(av,ca)
  fin
areas
  areaTrabajo: AreaPC (2,2,100,100)
  puestoJefe: AreaP (1,1,1,1)
  puesto1: AreaP (2,1,2,1)
  puesto2: AreaP (3,1,3,1)
robots
  robot trabajador
  variables
    flores:numero
    papeles:numero
    tareaNum:numero
    av:numero
    ca:numero  
  comenzar
    flores := 0
    papeles := 0
    RecibirMensaje(tareaNum,jefe)
    mientras(tareaNum <> 4)
      RecibirMensaje(av,jefe)
      RecibirMensaje(ca,jefe)
      procesarEsquina(tareaNum,av,ca,flores,papeles)
      {EnviarMensaje(V,jefe)}
      RecibirMensaje(tareaNum,jefe)
  fin
  robot Jefe
  variables
    numTarea : numero
    numEmpleado : numero
    esperar : boolean
    av: numero
    ca: numero
  comenzar
    {Asignacion de tareas}
    repetir 10
      {Generacion de datos}
      Random(numEmpleado,1,2)
      Random(numTarea,1,3)
      Random(av,2,100)
      Random(ca,2,100)
      {Envio de datos}
      si(numEmpleado = 1)
        EnviarMensaje(numTarea,t1)
        EnviarMensaje(av,t1)
        EnviarMensaje(ca,t1)
      sino
        EnviarMensaje(numTarea,t2)
        EnviarMensaje(av,t2)
        EnviarMensaje(ca,t2)
      {Espera}
      {RecibirMensaje(esperar,*)}
    EnviarMensaje(4,t1)
    EnviarMensaje(4,t2)
  fin
variables
  t1 : trabajador
  t2 : trabajador
  jefe : Jefe
comenzar
  AsignarArea(t1, areaTrabajo)
  AsignarArea(t1, puesto1)
  AsignarArea(t2, areaTrabajo)
  AsignarArea(t2, puesto2)
  AsignarArea(jefe, puestoJefe)
  Iniciar(jefe, 1,1)
  Iniciar(t1, 2,1)
  Iniciar(t2, 3,1)
fin
