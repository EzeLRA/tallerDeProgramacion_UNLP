{Se organizo una competencia entre el equipo A y el equipo B. Cada equipo consta de dos robots deben realizar por etapas el perimetro de un cuadrado de (10x10),recogiendo todas las flores.Cuando el primer robot de cada equipo completa su etapa(primera parte del recorrido) debe avisarle a su companiero para que continue con la segunda etapa.Ademas , cuando un robot finaliza su etapa debe depositar de a una las flores juntadas en la esquina(20,20).Al finalizar la competencia , un robot fiscalizador debe informar la cantidad de flores del equipo que junto mas flore}
programa parcial3
procesos
  proceso juntarFlores(ES cant:numero)
  comenzar
    mientras HayFlorEnLaEsquina
      tomarFlor
      cant := cant + 1
  fin
  proceso recorrerHasta(E tam:numero;ES cant:numero)
  comenzar
    repetir tam
      juntarFlores(cant)
      mover
  fin
  proceso almacenarFlor
  variables
    avIni,caIni:numero
  comenzar
    avIni:=PosAv
    caIni:=PosCa
    BloquearEsquina(20,20)
    Pos(20,20)
    si HayFlorEnLaBolsa
      depositarFlor
    Pos(avIni,caIni)
    LiberarEsquina(20,20)
  fin
areas
  almacen: AreaPC(20,20,20,20)
  {A}
  area1a: AreaP(1,1,1,11)
  area2a: AreaP(2,11,11,11)
  area3a: AreaP(11,1,11,10)
  area4a: AreaP(2,1,10,1)
  {B}
  area1b: AreaP(13,1,13,11)
  area2b: AreaP(14,11,23,11)
  area3b: AreaP(23,1,23,10)
  area4b: AreaP(14,1,22,1)
  puesto: AreaP(30,1,30,1)
robots
  robot participante1
  variables
    flores:numero
    id:numero
  comenzar
    flores:=0
    RecibirMensaje(id,jefe)
    repetir 2
      recorrerHasta(10,flores)
      derecha
    si(id=1)
      EnviarMensaje(flores,robiA2)
    sino
      EnviarMensaje(flores,robiB2)
    repetir flores
      almacenarFlor
  fin
  robot participante2
  variables
    id,flores:numero
  comenzar
    RecibirMensaje(id,jefe)
    repetir 2
      derecha
    si(id=1)
      RecibirMensaje(flores,robiA1)
    sino
      RecibirMensaje(flores,robiB1)
    repetir 2
      recorrerHasta(9,flores)
      derecha 
    EnviarMensaje(id,jefe)
    EnviarMensaje(flores,jefe)
    repetir flores
      almacenarFlor
  fin
  robot Jefe
  variables
    ids,cant:numero
    idMax,cantMax:numero
  comenzar
    EnviarMensaje(1,robiA1)
    EnviarMensaje(1,robiA2)
    EnviarMensaje(2,robiB1)
    EnviarMensaje(2,robiB2)
    cantMax := -1
    repetir 2
      RecibirMensaje(ids,*)
      si(ids=1)
        RecibirMensaje(cant,robiA2)
      sino
        RecibirMensaje(cant,robiB2)
      si(cantMax < cant)
        cantMax := cant
        idMax := ids
    Informar(idMax)
    Informar(cantMax)
  fin
variables
  robiA1:participante1
  robiA2:participante2
  robiB1:participante1
  robiB2:participante2
  jefe:Jefe
comenzar
  {A}
  AsignarArea(robiA1, area1a)
  AsignarArea(robiA1, area2a)
  AsignarArea(robiA2, area3a)
  AsignarArea(robiA2, area4a)
  {B}
  AsignarArea(robiB1, area1b)
  AsignarArea(robiB1, area2b)
  AsignarArea(robiB2, area3b)
  AsignarArea(robiB2, area4b)
  {almacen}
  AsignarArea(robiA1, almacen)
  AsignarArea(robiA2, almacen)
  AsignarArea(robiB1, almacen)
  AsignarArea(robiB2, almacen)
  {jefe}
  AsignarArea(jefe, puesto)
  Iniciar(robiA1, 1,1)
  Iniciar(robiA2, 11,10)
  Iniciar(robiB1, 13,1)
  Iniciar(robiB2, 23,10)
  Iniciar(jefe,30,1)
fin