programa parcial6
procesos
  proceso juntarFlores(ES flores:numero)
  comenzar
    mientras HayFlorEnLaEsquina
      tomarFlor
      flores := flores + 1
  fin
  proceso recorrer(E tam:numero;ES flores: numero)
  comenzar
    repetir tam
      juntarFlores(flores)
      mover
  fin
  proceso dejarFloresEn(ES flores:numero)
  variables
    avIni:numero
    caIni:numero
  comenzar
    avIni:=PosAv
    caIni:=PosCa
    repetir flores
      BloquearEsquina(10,10)
      Pos(10,10)
      depositarFlor
      Pos(avIni,caIni)
      LiberarEsquina(10,10)
  fin
areas
  almacen: AreaPC (10,10,10,10)
  area1: AreaP (1,1,6,6)
  area2: AreaP (7,1,12,6)
  area3: AreaP (13,1,18,6)
  puesto: AreaP (20,1,20,1)
robots
  robot Trabajador
  variables
    tam,flores,id:numero
    avanzar:boolean
  comenzar
    flores:=0
    RecibirMensaje(id,jefe)
    RecibirMensaje(tam,jefe)
    repetir 3
      recorrer(tam,flores)
      derecha
      EnviarMensaje(V,jefe)
      RecibirMensaje(avanzar,jefe)
    recorrer(tam,flores)
    derecha
    EnviarMensaje(id,jefe)
    EnviarMensaje(flores,jefe)
    RecibirMensaje(avanzar,jefe)
    si(avanzar)
      dejarFloresEn(flores)
  fin
  robot Jefe
  variables
    ids,cant:numero
    idMax,cantMax:numero
    esperar:boolean
  comenzar
    {ids}
    EnviarMensaje(1,trabajador1)
    EnviarMensaje(2,trabajador2)
    EnviarMensaje(3,trabajador3)
    {tamanio}
    EnviarMensaje(5,trabajador1)
    EnviarMensaje(5,trabajador2)
    EnviarMensaje(5,trabajador3)
    repetir 3
      repetir 3
        RecibirMensaje(esperar,*)
      EnviarMensaje(V,trabajador1)
      EnviarMensaje(V,trabajador2)
      EnviarMensaje(V,trabajador3)
    repetir 3
      RecibirMensaje(ids,*)
      si(ids=1)
        RecibirMensaje(cant,trabajador1)
      sino
        si(ids=2)
          RecibirMensaje(cant,trabajador2)
        sino
          RecibirMensaje(cant,trabajador3)
      si(cant>cantMax)
        cantMax := cant
        idMax := ids
    Informar(idMax)
    Informar(cantMax)
    si(idMax=1)
      EnviarMensaje(F,trabajador1)
      EnviarMensaje(V,trabajador2)
      EnviarMensaje(V,trabajador3)
    sino
      si(idMax=2)
        EnviarMensaje(V,trabajador1)
        EnviarMensaje(F,trabajador2)
        EnviarMensaje(V,trabajador3)
      sino
        EnviarMensaje(V,trabajador1)
        EnviarMensaje(V,trabajador2)
        EnviarMensaje(F,trabajador3) 
  fin
variables
  trabajador1: Trabajador
  trabajador2: Trabajador
  trabajador3: Trabajador
  jefe: Jefe
comenzar
  AsignarArea(trabajador1, area1)
  AsignarArea(trabajador2, area2)
  AsignarArea(trabajador3, area3)
  AsignarArea(trabajador1, almacen)
  AsignarArea(trabajador2, almacen)
  AsignarArea(trabajador3, almacen)
  AsignarArea(jefe, puesto)
  Iniciar(trabajador1,1,1)
  Iniciar(trabajador2,7,1)
  Iniciar(trabajador3,13,1)
  Iniciar(jefe, 20,1)
fin