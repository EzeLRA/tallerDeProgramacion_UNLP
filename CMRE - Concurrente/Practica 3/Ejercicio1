programa ejercicio1
procesos
  proceso recolectarFlores(ES flores: numero)
  comenzar
    mientras HayFlorEnLaEsquina
      tomarFlor
      flores := flores + 1
  fin
  proceso recolectarPapeles(ES papeles:numero)
  comenzar
    mientras HayPapelEnLaEsquina
      tomarPapel
      papeles := papeles + 1
  fin
  proceso depositarFlores(E flores:numero)
  comenzar
    repetir flores
      depositarFlor
  fin
  proceso depositarPapeles(E papeles:numero)
  comenzar
    repetir papeles
      depositarPapel
  fin
  proceso ingresarAreaRango(E x1:numero;E x2:numero;E y1:numero;E y2:numero)
  variables
    X:numero
    Y:numero
  comenzar
    Random(X,x1,x2)
    Random(Y,y1,y2)
    Pos(X,Y)
  fin
areas
  areaFlores: AreaPC (1,1,5,10)
  areaPapeles: AreaPC (6,1,10,9)
  almacen: AreaC(10,10,10,10)
  puestoF1: AreaP (6,10,6,10)
  puestoF2: AreaP (7,10,7,10)
  puestoP1: AreaP (8,10,8,10)
  puestoP2: AreaP (9,10,9,10)
robots
  robot florero1
  variables
    avIni : numero
    caIni : numero
    flores : numero
    intentos : numero
  comenzar
    flores := 0
    intentos := 5
    avIni := PosAv
    caIni := PosCa
    mientras(intentos > 0)
      {Ingreso al area}
      ingresarAreaRango(1,5,1,10)
      {Procesado del area}
      recolectarFlores(flores)
      {Regreso al puesto}
      intentos := intentos - 1
      {Aviso al robor companiero}
      Pos(avIni,caIni)
      EnviarMensaje(intentos,floreroB)
      si(intentos > 0)
        RecibirMensaje(intentos,floreroB)
    {Ingreso a la esquina 10,10}
    BloquearEsquina(10,10)
    Pos(10,10)
    depositarFlores(flores)
    Pos(avIni,caIni)
    LiberarEsquina(10,10)
  fin
  robot florero2
  variables
    avIni : numero
    caIni : numero
    flores : numero
    intentos : numero
  comenzar
    flores := 0
    avIni := PosAv
    caIni := PosCa
    {Espera para ingresar}
    RecibirMensaje(intentos,floreroA)
    mientras(intentos > 0)
      {Ingreso al area}
      ingresarAreaRango(1,5,1,10)
      {Procesado del area}
      recolectarFlores(flores)
      {Regreso al puesto}
      intentos := intentos - 1
      Pos(avIni,caIni)
      {Aviso al robot companiero}
      EnviarMensaje(intentos,floreroA)
      si(intentos > 0)
        RecibirMensaje(intentos,floreroA)
    {Ingreso a la esquina 10,10}
    BloquearEsquina(10,10)
    Pos(10,10)
    depositarFlores(flores)
    Pos(avIni,caIni)
    LiberarEsquina(10,10)
  fin
  robot papelero1
  variables
    avIni : numero
    caIni : numero
    papeles : numero
    intentos : numero
  comenzar
    papeles := 0
    intentos := 3
    avIni := PosAv
    caIni := PosCa
    mientras(intentos > 0)
      {Ingreso al area}
      ingresarAreaRango(6,10,1,9)
      {Procesado del area}
      recolectarPapeles(papeles)
      {Regreso al puesto}
      intentos := intentos - 1
      {Aviso al robor companiero}
      Pos(avIni,caIni)
      EnviarMensaje(intentos,papeleroB)
      si(intentos > 0)
        RecibirMensaje(intentos,papeleroB) 
    {Ingreso a la esquina 10,10}
    BloquearEsquina(10,10)
    Pos(10,10)
    depositarPapeles(papeles)
    Pos(avIni,caIni)
    LiberarEsquina(10,10)
  fin
  robot papelero2
  variables
    avIni : numero
    caIni : numero
    papeles : numero
    intentos : numero
  comenzar
    papeles := 0
    avIni := PosAv
    caIni := PosCa
    {Espera para ingresar}
    RecibirMensaje(intentos,papeleroA)
    mientras(intentos > 0)
      {Ingreso al area}
      ingresarAreaRango(6,10,1,9)
      {Procesado del area}
      recolectarPapeles(papeles)
      {Regreso al puesto}
      intentos := intentos - 1
      Pos(avIni,caIni)
      {Aviso al robot companiero}
      EnviarMensaje(intentos,papeleroA)
      si(intentos > 0)
        RecibirMensaje(intentos,papeleroA)
    {Ingreso a la esquina 10,10}
    BloquearEsquina(10,10)
    Pos(10,10)
    depositarPapeles(papeles)
    Pos(avIni,caIni)
    LiberarEsquina(10,10)
  fin
variables
  floreroA: florero1
  floreroB: florero2
  papeleroA: papelero1
  papeleroB: papelero2
comenzar
  AsignarArea(floreroA, almacen)
  AsignarArea(floreroB, almacen)
  AsignarArea(papeleroA, almacen)
  AsignarArea(papeleroB, almacen)
  AsignarArea(floreroA, areaFlores)
  AsignarArea(floreroB, areaFlores)
  AsignarArea(papeleroA, areaPapeles)
  AsignarArea(papeleroB, areaPapeles)
  AsignarArea(floreroA, puestoF1)
  AsignarArea(floreroB, puestoF2)
  AsignarArea(papeleroA, puestoP1)
  AsignarArea(papeleroB, puestoP2)
  Iniciar(floreroA, 6,10)
  Iniciar(floreroB, 7,10)
  Iniciar(papeleroA, 8,10)
  Iniciar(papeleroB, 9,10)
fin